// gradle build script for the aim42 documentation
//
// free software - without guarantee, use at your own risk
// ========================================================

println """compiling with Java ${System.getProperty("java.version")}"""
println "building project version " + project.project_version


buildscript {
    // these are the BUILDSCRIPT deps - required to execute
    // build targets and -operations
    repositories {
        maven {
            name 'Bintray Asciidoctor repo'
            url  'http://dl.bintray.com/content/aalmiray/asciidoctor'
        }
        maven {
            name 'Bintray Asciidoctor repo'
            url  'http://dl.bintray.com/content/aalmiray/asciidoctor'
        }
   
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath ('org.aim42:HtmlSanityCheck-gradle-plugin:0.5.3')
//        classpath ('org.aim42:HtmlSanityCheck-core:0.8.0')
        classpath ( group: 'org.asciidoctor',
                    name: 'asciidoctor-gradle-plugin',
                    version: '1.5.2')
    }
}


ext {
    srcDir  = "$projectDir/src/main"
    targetDir = "$buildDir/docs"
}

// =========== asciidoctor stuff ===============
apply plugin: 'org.asciidoctor.convert'

asciidoctor {
    outputDir = file( targetDir )
    sourceDir = file("$srcDir/asciidoc")
    sources  { include "index.adoc" }
    //options separateOutputDirs=false
    attributes = [
            separateOutputDirs: false,
            imagesdir   : 'images',
            doctype: 'book',
            icons: 'font',
            toc: 'left',
            sectlink: true,
            sectanchors: true,
            numbered: true,
            'source-highlighter': 'coderay',
            'coderay-css': 'class',
            stylesheet: 'aim42.css',
            stylesdir: "$srcDir/resources/css/",  
            version: project.project_version 
    ]
    resources {
        from "$srcDir/resources/images"
        into 'images'
        
    } 
}



defaultTasks 'aim42'




task copyResources(
    type: Copy,
    description: 'copy (downloadable) resources and artifacts to resource folder'
) {
    from "$srcDir/resources/docs"
    into "$targetDir/html5/resources"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

task aim42(
    dependsOn: [copyResources, asciidoctor],
    description: 'collects all required tasks for creating aim42 documentation'
) << { }



task publish(type: GradleBuild) {
    String travisPr = System.getenv('TRAVIS_PULL_REQUEST')
    if ("false".equals(travisPr)) {
        buildFile = 'publish.gradle'
        tasks = ['publishGhPages']
    } else {
        println 'env var TRAVIS_PULL_REQUEST says we build for PR #' + travisPr + ', won\'t publish docs!'
    }
}


// ============ validation

apply plugin: 'org.aim42.HtmlSanityCheck-gradle-plugin'
htmlSanityCheck {

  // ensure asciidoctor->html runs first
  // and images are copied to build directory
  dependsOn asciidoctor

  sourceDir = new File( "$targetDir/html5" )

  // files to check, in Set-notation
  sourceDocuments = [ "index.html"]

  // where to put results of sanityChecks...
  checkingResultsDir = new File( "$buildDir/report/htmlchecks" )

  // false: restrict checks to local resources
  // true:  also check external (e.g. http, https...) links.
  checkExternalLinks = false
}
